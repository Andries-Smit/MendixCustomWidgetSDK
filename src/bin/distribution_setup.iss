; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Mendix Custom Widget SDK"
#define MyAppVersion "1.0.0"
#define MyAppPublisher "Mendix Technology B.V."
#define MyAppURL "http://www.mendix.com/"
#define MyAppExeName "MendixWidgets.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A96E4B0B-B6EC-4613-BCC5-944D00CC8871}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=C:\Projects\GitHub\MendixCustomWidgetSDK2\src\LICENSE.txt
OutputDir=C:\Projects\GitHub\MendixCustomWidgetSDK2\src\bin\Dist
OutputBaseFilename=setup
SetupIconFile=C:\Projects\GitHub\MendixCustomWidgetSDK2\src\app2.ico
Compression=lzma
SolidCompression=yes
WizardImageFile=widget_setup_left.bmp
WizardSmallImageFile=widget_setup_right_top.bmp

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Files]          
Source: "C:\Projects\GitHub\MendixCustomWidgetSDK2\src\bin\Release\file.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Projects\GitHub\MendixCustomWidgetSDK2\src\bin\Release\MendixWidgets.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Projects\GitHub\MendixCustomWidgetSDK2\src\bin\Release\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "C:\Projects\GitHub\MendixCustomWidgetSDK2\src\bin\Dependencies\dotnet452.exe"; AfterInstall: InstallFramework; DestDir: {tmp}; Flags: deleteafterinstall; Check: FrameworkIsNotInstalled    
Source: "C:\Projects\GitHub\MendixCustomWidgetSDK2\src\bin\Dependencies\7z920.exe"; AfterInstall: InstallSevenZip; DestDir: {tmp}; Flags: deleteafterinstall; Check: SevenZipIsNotInstalled

[Code]
procedure InstallFramework;
var
  ResultCode: Integer;  
  StatusText: string;
begin
  StatusText := WizardForm.StatusLabel.Caption;
  WizardForm.StatusLabel.Caption := 'Installing .NET framework...';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    if not Exec(ExpandConstant('{tmp}\dotnet452.exe'), '/q /noreboot', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
    begin
      // you can interact with the user that the installation failed
      MsgBox('.NET installation failed with code: ' + IntToStr(ResultCode) + '.',
        mbError, MB_OK);
    end;
  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;
  end;
end;

function FrameworkIsNotInstalled: Boolean;
begin
    Result := not RegKeyExists(HKEY_LOCAL_MACHINE, 'Software\Microsoft\.NETFramework\policy\v4.0');
end;

procedure InstallSevenZip;
var
  ResultCode: Integer;  
  StatusText: string;
begin
  StatusText := WizardForm.StatusLabel.Caption;
  WizardForm.StatusLabel.Caption := 'Installing 7-Zip...';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    if not Exec(ExpandConstant('{tmp}\7z920.exe'), '/S', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
    begin
      // you can interact with the user that the installation failed
      MsgBox('7-Zip installation failed with code: ' + IntToStr(ResultCode) + '.',
        mbError, MB_OK);
    end;
  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;
  end;
end;

function SevenZipIsNotInstalled: Boolean;
begin
    Result := not RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\7-Zip');
end;

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon
    
[Registry]
Root: HKCR; Subkey: ".mws"; ValueType: string; ValueName: ""; ValueData: "MendixCustomWidgetSDK"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "MendixCustomWidgetSDK"; ValueType: string; ValueName: ""; ValueData: "Mendix Custom Widget SDK File"; Flags: uninsdeletekey
Root: HKCR; Subkey: "MendixCustomWidgetSDK\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\file.ico"
Root: HKCR; Subkey: "MendixCustomWidgetSDK\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\MendixWidgets.exe"" ""%1""" 

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

